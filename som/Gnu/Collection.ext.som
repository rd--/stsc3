Collection = Collection (

* arg = (
	^arg adaptToCollection: self andSend: #*
)

+ arg = (
	^arg adaptToCollection: self andSend: #+
)

- arg = (
	^arg adaptToCollection: self andSend: #-
)

/ arg = (
	^arg adaptToCollection: self andSend: #/
)

// arg = (
	^arg adaptToCollection: self andSend: #//
)

\\ arg = (
	^arg adaptToCollection: self andSend: #\\
)

adaptToCollection: rcvr andSend: selector = (
	rcvr isSequenceable & self isSequenceable ifFalse: [self error: 'Only sequenceable collections may be combined arithmetically'].
	^rcvr withExtending: self collect: [:rcvrElement :myElement | rcvrElement perform: selector with: myElement]
)

adaptToNumber: rcvr andSend: selector = (
	^ self collect: [:element | rcvr perform: selector with: element]
)

any: numberOfElements = (
	^ self any: numberOfElements as: self species
)

any: numberOfElements as: aClass = (
	| index result |
	index := 0.
	result := aClass new: numberOfElements.
	result fillFrom: self with: [:each |
		(index := index + 1) > numberOfElements
			ifTrue: [^ result]
			ifFalse: [each]].
	index = numberOfElements
		ifFalse: [self error: 'Not enough elements in this collection.'].
	^ result
)

collect: aBlock as: aClass = (
	^(aClass new: self size) fillFrom: self with: aBlock
)

fillFrom: aCollection with: aBlock = (
	aCollection do: [ :each |
		self add: (aBlock value: each) ]
)

histogramOf: aBlock = (
	^ self collect: aBlock as: Bag
)

isCollection = (
	^true
)

ceiling = (
	^self collect: [ :each | each ceiling ]
)

log2 = (
	^self collect: [ :each | each log2 ]
)

max  = (
	^ self inject: self anyOne into: [:min :each | min max: each]
)

min  = (
	^ self inject: self anyOne into: [:min :each | min min: each]
)

raisedTo: arg = (
	^arg adaptToCollection: self andSend: #raisedTo:
)

reduce: binaryBlock = (
	| first nextValue |
	first := true.
	self do: [ :each |
		first
			ifTrue: [ nextValue := each. first := false ]
			ifFalse: [ nextValue := binaryBlock value: nextValue value: each ] ].
	first ifTrue: [ self errorEmptyCollection ].
	^nextValue
)

reciprocal = (
	^self collect: [ :item | item reciprocal ]
)

sqrt = (
	^self collect: [ :item | item sqrt ]
)

sum = (
	^self inject: 0 into: [ :total :item | total + item ]
)

----

newFrom: aCollection = (
	^ self withAll: aCollection
)

)
