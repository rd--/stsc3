Ugen = Object (

| scUgen port |

** aNumber = (
	^self raisedTo: aNumber
)

@ aUgen = (
	^Point x: self y: aUgen
)

adaptToCollection: rcvr andSend: selector = (
	^rcvr collect: [ :item | item perform: selector with: self]
)

adaptToFloat: rcvr andSend: selector = (
	^BinaryOpUgen selector: selector a: rcvr b: self
)

adaptToFraction: rcvr andSend: selector = (
	^BinaryOpUgen selector: selector a: rcvr b: self
)

adaptToInteger: rcvr andSend: selector = (
	^BinaryOpUgen selector: selector a: rcvr b: self
)

add: x = (
	  ^x = 0 ifTrue: [ self ] ifFalse: [ self + x ]
)

addToUgenSet: c protectedBy: w = (
	scUgen addToUgenSet: c protectedBy: w
)

clearBuf = (
	"ClearBuf does not copy the buffer number through so this is an Mrg node."
	^self mrg: (ClearBuf buf: self)
)

draw = (
	Sc3 draw: self
)

isOutUgen = (
	^scUgen name = 'Out'
)

isUgen = (
	^true
)

isZero = (
	^false
)

kr = (
	scUgen kr.
	^self
)

lag: lagTime = (
	^Lag in: self lagTime: lagTime
)

mrg: rhs = (
	scUgen mrg: rhs.
	^self
)

mul: x = (
	  ^x = 1 ifTrue: [ self ] ifFalse: [ self * x ]
)

mul: mul add: add = (
	^(self mul: mul) add: add
)

play = (
	Sc3 play: self
)

port = (
	^port
)

printUgens = (
	Sc3 printUgens: self
)

rate = (
	^scUgen rate
)

rounded = (
	^self roundTo: 1.0
)

scUgen = (
	^scUgen
)

scUgen: anScUgen port: anInteger = (
	scUgen := anScUgen.
	port := anInteger
)

splay2 = (
	^Splay2 inArray: self
)

unitCps = (
	^(self * 127) midiCps
)

----------------------------

scUgen: anScUgen port: anInteger = (
	^self new scUgen: anScUgen port: anInteger
)

comment = (
	^'Output proxy'
)

)
