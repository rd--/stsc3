// modal space (jmcc) #8
var b = #[0, 2, 3.2, 5, 7, 9, 10].asLocalBuf;
var k = DegreeToKey(b, MouseX(0, 15, 0, 0.1), 12);
var c = {
	arg n, r;
	var o = SinOsc((r + k + (n * 0.04)).midiCps, 0) * 0.1;
	var t = LFPulse(#[48, 55].midiCps, 0, 0.15);
	var f = (SinOsc(0.1, 0) * 10 + r).midiCps;
	var d = RLPF(t, f, 0.1) * 0.1;
	var m = o + d;
	CombN(m, 0.31, 0.31, 2) + m
};
var n = LFNoise1([3, 3]);
(c.value(n, 48) + c.value(n, 72)) * 0.25

//---- modal space (jmcc) #8 ; filtermethods
var b = #[0, 2, 3.2, 5, 7, 9, 10].asLocalBuf;
var k = MouseX(0, 15, 0, 0.1).degreeToKey(b, 12);
var c = {
	arg n, r;
	var o = SinOsc((k + r + (n * 0.04)).midiCps, 0).mul(0.1);
	var t = LFPulse(#[48, 55].midiCps, 0, 0.15);
	var f = SinOsc(0.1, 0).mulAdd(10, r).midiCps;
	var d = t.rlpf(f, 0.1).mul(0.1);
	var m = o + d;
	m.combN(0.31, 0.31, 2) + m
};
var n = LFNoise1([3, 3]);
c.value(n, 48) + c.value(n, 72) * 0.25
