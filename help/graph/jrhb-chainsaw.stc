// chainsaw (jrhb)
var f = {
    arg saw;
	var rate = ExpRand(0.1, 2);
	var freq = 0.6.coin.if({ LFNoise1(rate.dup).exprange(0.01, 10)}, { LFNoise1(rate.dup).exprange(10, 50) });
	var u1 = LFSaw(LinExp(saw, -1, 1, freq, freq * LFNoise1(rate.dup).exprange(2, 10)), 0);
	var u2 = 0.5.coin.if({ u1 * [1 - saw, saw.reverse].choose }, { u1 * (LFSaw(freq * 0.1, 0) * 0.1 + 1) });
	u2.clip2(1.0)
};
var compose = { arg f, g; { arg x; f.value(g.value(x)) } };
var g = {
    arg func, n;
	var r = func;
    n.timesRepeat({ r = compose.value(r, r) });
	r
};
var freq = g.value(f, 4).value(LFSaw(0.2 * [1, 1.1], 0)).exprange(6, 11000);
var osc = BPF(Saw(freq).product, [70, 800, 9000, 5242], 0.2).mix;
[osc, osc] * 0.3
