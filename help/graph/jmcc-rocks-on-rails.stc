// rocks on rails ; jmcc ; graph variant
var p = 20; // number of partials
var n = 4; // number of overlapping events
OverlapTexture({
	arg tr;
	Pan2(
		DynRingzBank( // p resonant modes
			Resonz(
				Dust(100) * 0.04, // excitation
				XLn(3000, 300, 8), // sweep filter down
				0.2 // band width ratio
			),
			{ 200 + TLinRand(0, 3000, 0, tr) }.dup(p), // resonant frequencies
			[1], // amplitudes
			{ 0.2 + TRand(0, 1, tr) }.dup(p) // ring times
		),
		Ln(TRand(-1, 1, tr), TRand(-1, 1, tr), 8), // sweep pan
		1
	)
}, 2, 3, n).sum * 0.5

//---- rocks on rails ; jmcc
var p = 20; // number of partials
var n = 4; // number of overlapping events
Texture.overlap({
	Pan2(
		RingzBank( // p resonant modes
			Resonz(
				Dust(100) * 0.04, // excitation
				XLn(3000, 300, 8), // sweep filter down
				0.2 // band width ratio
			),
			{ 200 + 3000.0.linrand }.dup(p), // resonant frequencies
			nil, // amplitudes default to 1.0
			{ 0.2 + 1.0.rand }.dup(p) // ring times
		),
		Ln(1.0.rand2, 1.0.rand2, 8), // sweep pan
		1
	)
}, 2, 3, n, 2)
