// shift register (dm) - https://sccode.org/1-590
var shift_register = {
    arg length, trig, in;
    var buf = LocalBuf(1, length);
    var count = PulseCount(trig, 0);
    Demand(trig, 0, Dbufrd(buf, count + length.iota, 1)).reverse.mrg(Demand(trig, 0, Dbufwr(buf, count, in, 1)))
};
var amp = 0.1;
var pulse = Impulse(1 / 16, 0);
var rate = TChoose(pulse, #[3, 5, 10]);
var trans = TChoose(pulse, #[0, 2, -2, 7, -5]);
var trig = Trig1(CuspL(rate.kr * 3, 1, 1.9, 0), 0.001);
var octave = Demand(PulseDivider(trig, 4, 0), 0, Drand(inf, #[12, -12]));
var note = Demand(trig, 0, Dseq(inf, [42, 46, 51, 54, 59, 63, 66].shuffled + trans.kr) + octave);
var chord = shift_register.value(5, trig, note);
var chord_cps = chord.midicps;
var cf = Vibrato(chord_cps, 6, 0.02, 0, 0, 0.04, 0.1, 0, 0);
var mf = chord_cps * LFPulse(1 / 8, 0, 0.5).range(1.01, 2.01);
var sig = PMOsc(cf, mf, EnvSpec(#[3, 3, 0], #[0, 0.2], #[-4], nil, nil, 0).asEnvGen(trig), 0);
var cmp = (sig * AmpCompA(chord_cps, 0, 0.32, 1) * amp).mix;
XFade2(cmp.dup, GVerb(BPF(cmp, 90.midicps, 1), 50, 8, 0.5, 0.5, 15, 0, 0.7, 0.5, 300), 0.2, 1)
