// Seq ; shared seq, different patterns ; spl
var a = Seq(inf, [1, 3, 2, 7, 8]);
var t = Impulse(5, 0);
var f = Demand(t, 0, [a, a]) * 30 + 340;
SinOsc(f,  0) * 0.1

// Seq ; distinct seq, equal patterns ; spl
var a = Seq(inf, [1, 3, 2, 7, 8]);
var b = Seq(inf, [1, 3, 2, 7, 8]);
var t = Impulse(5, 0);
var f = Demand(t, 0, [a, b]) * 30 + 340;
SinOsc(f,  0) * 0.1

// Seq ; at audio rate ; mouse control ; spl
var n = Seq(inf, [1, 3, 2, 7, 8, 32, 16, 18, 12, 24]);
var x = MouseX(1, 10000, 1, 0.1);
var t = Impulse(x, 0);
var f = DmdOn(t, 0, n) * 30 + 340;
SinOsc(f, 0) * 0.1

// Seq ; at control rate ; spl
var n = Seq(3, [1, 3, 2, 7, 8]);
var x = MouseX(1, 40, 1, 0.1);
var t = Impulse(x, 0);
var f = DmdOn(t, 0, n) * 30 + 340;
SinOsc(f.kr, 0) * 0.1

// Seq ; the SC2 Sequencer UGen is somewhat like the DmdOn & Seq idiom below ; spl
var tr = Impulse(6, 0);
var n0 = DmdOn(tr, 0, Seq(inf, [60, 62, 63, 58, 48, 55]));
var n1 = DmdOn(tr, 0, Seq(inf, [63, 60, 48, 62, 55, 58]));
LFSaw([n0, n1].midiCps, 0) * 0.05

// Seq ; rather than Mce expansion at tr, it can be clearer to view tr as a functor ; spl
var m = {
	arg f;
	var tr = Impulse(f, 0);
	var sq = Seq(inf, [60, 63, 67, 69]);
	DmdOn(tr, 0, sq).midiCps
};
SinOsc([2, 3, 5].collect(m), 0).Splay2 * 0.1

// Seq ; spl
var t = Impulse(2, 0);
var m = DmdOn(t, 0, Seq(inf, [55, 60, 63, 62, 60, 67, 63, 58]));
SinOsc(m.midiCps, 0) * 0.1
